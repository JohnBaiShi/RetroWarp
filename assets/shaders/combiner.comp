#version 450
#extension GL_EXT_shader_16bit_storage : require
#extension GL_EXT_shader_8bit_storage : require
#extension GL_EXT_samplerless_texture_functions : require

layout(local_size_x = 16, local_size_y = 16) in;

#include "constants.h"
#include "primitive_setup.h"
#include "fb_info.h"

layout(std430, set = 0, binding = 0) readonly buffer WorkList
{
    u16vec4 tile_raster_work[];
};

struct ColorTile
{
    u8vec4 color[TILE_HEIGHT * TILE_WIDTH];
};

struct DepthTile
{
    uint16_t depth[TILE_HEIGHT * TILE_WIDTH];
};

struct FlagTile
{
    uint16_t flag[TILE_HEIGHT * TILE_WIDTH];
};

layout(std430, set = 0, binding = 1) writeonly buffer Color
{
    ColorTile color_tiles[];
};

layout(std430, set = 0, binding = 2) writeonly buffer Depth
{
    DepthTile depth_tiles[];
};

layout(std430, set = 0, binding = 3) writeonly buffer Flags
{
    FlagTile flag_tiles[];
};

layout(std430, set = 0, binding = 4) readonly buffer TriangleSetupPos
{
    PrimitiveSetupPos primitives_pos[];
};

layout(std430, set = 0, binding = 5) readonly buffer TriangleSetupAttr
{
    PrimitiveSetupAttr primitives_attr[];
};

layout(set = 1, binding = 0) uniform utexture2D uTexture;

int quant_w(int w)
{
    return max((w + 8) >> 4, 1);
}

const int INVERSE_BITS = 4;
const int FIXED_LUT[(1 << INVERSE_BITS) + 1] = int[](
    -8388608,
    -7895160,
    -7456540,
    -7064090,
    -6710886,
    -6391320,
    -6100805,
    -5835553,
    -5592405,
    -5368709,
    -5162220,
    -4971026,
    -4793490,
    -4628197,
    -4473924,
    -4329604,
    -4194304
);

ivec2 fixed_uv_divider(ivec2 uv, int w)
{
    uint uw = uint(w);

    uint leading = 31u - uint(findMSB(uw));
    uw <<= leading;
    uw >>= (31 - INVERSE_BITS - 8);

    int rcp_frac = int(uw & 0xffu);
    uw >>= 8;
    uw &= uint((1 << INVERSE_BITS) - 1);

    int rcp = FIXED_LUT[uw] * (0x100 - rcp_frac) + FIXED_LUT[uw + 1u] * rcp_frac;

    ivec2 lo, hi;
    imulExtended(uv, ivec2(rcp), hi, lo);

    lo = ivec2(uvec2(lo) >> 21);
    hi = hi << 11;
    ivec2 res = -(lo | hi);

    int msb_index = 32 - int(leading);
    res = (res + (1 << (msb_index - 1))) >> msb_index;
    return res;
}

uvec4 filter_linear_horiz(uvec4 left, uvec4 right, uint weight)
{
    uint l = 32u - weight;
    uint r = weight;
    return left * l + right * r;
}

uvec4 filter_linear_vert(uvec4 top, uvec4 bottom, uint weight)
{
    uint t = 32u - weight;
    uint b = weight;
    return (top * t + bottom * b + 512u) >> 10u;
}

uvec4 multiply_unorm8(uvec4 a, uvec4 b)
{
    uvec4 v = a * b;
    v += (v >> 8u);
    v = (v + 0x80u) >> 8u;
    return v;
}

uint clamp_unorm16(int z)
{
    z = (z + 0x80) >> 8;
    return uint(clamp(z, 0, 0xffff));
}

ivec2 wrap_uv(ivec2 uv)
{
    return (uv << 11) >> 11;
}

void main()
{
    uint work_instance = gl_WorkGroupID.x;
    uvec4 work_item = uvec4(tile_raster_work[work_instance]);

    uint tile_x = work_item.x;
    uint tile_y = work_item.y;
    uint tile_instance_index = work_item.z;
    uint primitive_index = work_item.w;
    int x = int(tile_x * uint(TILE_WIDTH) + gl_LocalInvocationID.x);
    int y = int(tile_y * uint(TILE_HEIGHT) + gl_LocalInvocationID.y);
    int y_sub = y << SUBPIXELS_LOG2;

#define prim_pos primitives_pos[primitive_index]
#define prim_attr primitives_attr[primitive_index]

    int interpolation_base_x = prim_pos.x_a >> 16;
    int interpolation_base_y = int(prim_pos.y_lo);

    int span_begin_y = (int(prim_pos.y_lo) + ((1 << SUBPIXELS_LOG2) - 1)) >> SUBPIXELS_LOG2;
    int span_end_y = (int(prim_pos.y_hi) - 1) >> SUBPIXELS_LOG2;

    if (span_begin_y < fb_info.scissor.y)
        span_begin_y = fb_info.scissor.y;
    if (span_end_y >= fb_info.scissor.y + fb_info.scissor.w)
        span_end_y = fb_info.scissor.y + fb_info.scissor.w - 1;

    if (y < span_begin_y || y > span_end_y)
    {
        flag_tiles[tile_instance_index].flag[gl_LocalInvocationIndex] = uint16_t(0);
        return;
    }

    int x_a = prim_pos.x_a + prim_pos.dxdy_a * (y_sub - int(prim_pos.y_lo));
    int x_b = prim_pos.x_b + prim_pos.dxdy_b * (y_sub - int(prim_pos.y_lo));
    int x_c = prim_pos.x_c + prim_pos.dxdy_c * (y_sub - int(prim_pos.y_mid));

    // The secondary span edge is split into two edges.
    bool select_hi = y_sub >= int(prim_pos.y_mid);
    int primary_x = x_a;
    int secondary_x = select_hi ? x_c : x_b;

    int start_x, end_x;

    if ((int(prim_pos.flags) & PRIMITIVE_RIGHT_MAJOR_BIT) != 0)
    {
        start_x = (secondary_x + RASTER_ROUNDING) >> (16 + SUBPIXELS_LOG2);
        end_x = (primary_x - 1) >> (16 + SUBPIXELS_LOG2);
    }
    else
    {
        start_x = (primary_x + RASTER_ROUNDING) >> (16 + SUBPIXELS_LOG2);
        end_x = (secondary_x - 1) >> (16 + SUBPIXELS_LOG2);
    }

    if (start_x < fb_info.scissor.x)
    start_x = fb_info.scissor.x;
    if (end_x >= fb_info.scissor.x + fb_info.scissor.z)
    end_x = fb_info.scissor.x + fb_info.scissor.z - 1;

    if (x < start_x || x > end_x)
    {
        flag_tiles[tile_instance_index].flag[gl_LocalInvocationIndex] = uint16_t(0);
        return;
    }

    // We've passed the rasterization test. Interpolate colors, Z, 1/W.
    int dy = y_sub - interpolation_base_y;
    int dx = (x << SUBPIXELS_LOG2) - interpolation_base_x;

    ivec4 rgba = prim_attr.color + prim_attr.dcolor_dx * dx + prim_attr.dcolor_dy * dy;
    uvec4 urgba = uvec4(clamp((rgba + 0x80) >> 8, ivec4(0), ivec4(0xff)));

    ivec4 uvzw = prim_attr.uvzw + prim_attr.duvzw_dx * dx + prim_attr.duvzw_dy * dy;
    ivec2 uv = wrap_uv(uvzw.xy);
    uint z = clamp_unorm16(uvzw.z);
    int w = quant_w(uvzw.w);

    uv = fixed_uv_divider(uv, w);

    uv -= 16;
    ivec2 sub_uv = uv & 31;
    uv >>= 5;

    uv += ivec2(prim_attr.uv_offset);

    uvec4 tex_00 = texelFetch(uTexture, uv, 0);
    uvec4 tex_10 = texelFetchOffset(uTexture, uv, 0, ivec2(1, 0));
    uvec4 tex_01 = texelFetchOffset(uTexture, uv, 0, ivec2(0, 1));
    uvec4 tex_11 = texelFetchOffset(uTexture, uv, 0, ivec2(1, 1));

    uvec4 tex_0 = filter_linear_horiz(tex_00, tex_10, sub_uv.x);
    uvec4 tex_1 = filter_linear_horiz(tex_01, tex_11, sub_uv.x);
    uvec4 tex = filter_linear_vert(tex_0, tex_1, sub_uv.y);

    tex = multiply_unorm8(tex, urgba);

    color_tiles[tile_instance_index].color[gl_LocalInvocationIndex] = u8vec4(tex);
    depth_tiles[tile_instance_index].depth[gl_LocalInvocationIndex] = uint16_t(z);
    flag_tiles[tile_instance_index].flag[gl_LocalInvocationIndex] = uint16_t(1);
}
